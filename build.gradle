buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }

    }
    dependencies {
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.14.1'
    }
}

apply plugin: 'java'
apply plugin: 'org.jenkins-ci.jpi'

repositories {
    jcenter()
    maven {
        url "http://repo.jenkins-ci.org/releases/"
    }
    maven {
        url "https://m2proxy.atlassian.com/repository/public"
    }
}

group = 'org.jenkins-ci.plugins'
description = 'JIRA Trigger'

dependencies {
    compile('com.atlassian.jira:jira-rest-java-client-core:3.0.0') {
        exclude group: 'org.slf4j'
    }
    compile 'com.google.inject.extensions:guice-multibindings:4.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
    testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock
    testCompile 'org.objenesis:objenesis:1.2' // used by Spock
    testCompile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
}

jenkinsPlugin {
    coreVersion = '1.565.3'
    shortName = 'jira-trigger'
    displayName = 'JIRA Trigger Plugin'
    url = 'http://wiki.jenkins-ci.org/display/JENKINS/JIRA+Trigger+Plugin'
    gitHubUrl = 'https://github.com/ceilfors/jira-trigger-plugin'

    // enable injection of additional tests for checking the syntax of Jelly and other things
    disabledTestInjection = false

    // the output directory for the localizer task relative to the project root, defaults to the value shown
    localizerOutputDir = "${project.buildDir}/generated-src/localizer"

    fileExtension = 'jpi'

    developers {
        developer {
            id 'ceilfors'
            name 'Wisen Tanasa'
            email 'wisen@ceilfors.com'
        }
    }
}

// Split Acceptance Test as it requires JIRA. See README.md on how to run the acceptance test.
test {
    exclude 'com/ceilfors/**/*AcceptanceTest*'
}
task acceptanceTest(type: Test, dependsOn: 'generate-test-hpl') {
    include 'com/ceilfors/**/*AcceptanceTest*'
    reports.html.destination = file("$buildDir/reports/acceptanceTests")
}
